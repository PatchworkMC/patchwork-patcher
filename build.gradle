plugins {
	id 'application'
	id 'eclipse'
	id 'idea'
	id 'maven-publish'
	id 'checkstyle'
	id 'com.jfrog.bintray' version '1.8.4'
}

sourceCompatibility = 1.8

def ENV = System.getenv()

if (ENV.bintray_user && ENV.bintray_key) {
	def versionName = System.getenv("GITHUB_REF")
	versionName = versionName.substring(10)
	if (versionName.startsWith("v")) {
		versionName = versionName.substring(1) //strip "v" in "v1.0.0" etc.
	}
	version = versionName
} else if (ENV.GITHUB_SHA) {
	version = System.getenv("GITHUB_SHA").substring(0, 9)
} else {
	version = 'LOCAL'
}

group = 'com.patchworkmc'
archivesBaseName = 'patchwork-patcher'

checkstyle {
	configFile = rootProject.file("checkstyle.xml")
	toolVersion = '8.25'
}

repositories {
	mavenCentral()
	maven {
		name = 'Fabric'
		url = 'http://maven.modmuss50.me/'
	}
	maven {
		url 'https://dl.bintray.com/patchworkmc/Patchwork-Maven/'
	}
}

dependencies {
	// ASM
	implementation 'org.ow2.asm:asm:7.1'
	implementation 'org.ow2.asm:asm-analysis:7.1'
	implementation 'org.ow2.asm:asm-commons:7.1'
	implementation 'org.ow2.asm:asm-tree:7.1'
	implementation 'org.ow2.asm:asm-util:7.1'

	// Patchwork
	implementation 'net.patchworkmc:patchwork-manifest:0.5.0'

	// Logging
	implementation 'org.apache.logging.log4j:log4j-api:2.13.2'
	implementation 'org.apache.logging.log4j:log4j-core:2.13.2'
	runtimeOnly 'net.minecrell:terminalconsoleappender:1.2.0'
	runtimeOnly 'org.jline:jline-terminal-jansi:3.12.1'

	// Other dependencies
	implementation 'net.fabricmc:tiny-remapper:0.2.1.62'

	implementation 'com.electronwill.night-config:toml:3.6.0'

	implementation 'com.google.code.gson:gson:2.8.5'

	implementation 'org.fusesource.jansi:jansi:1.18'

	implementation "commons-io:commons-io:2.6"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	archiveClassifier.set 'sources'
	from sourceSets.main.allSource
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId 'com.patchworkmc'
			artifactId "patchwork"
			version version

			artifact (jar)
			artifact (sourcesJar)
		}
	}
}

application {
	mainClassName "com.patchworkmc.PatchworkUI"
	if (file('.classpath').exists()) {
		println("Eclipse detected. Disabling ANSI.")
		applicationDefaultJvmArgs = ['-Dpatchwork.log.disableAnsi=true']
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

def runDir = file("run")

if(!runDir.exists() && !runDir.mkdirs()) {
	throw new IOException("Failed to create run directory ${runDir.getAbsolutePath()}")
}

run {
	workingDir runDir
}

jar {
    manifest {
        attributes(
				'Implementation-Title': 'Patchwork Patcher',
				'Implementation-Version': archiveVersion,
                'Main-Class': 'com.patchworkmc.PatchworkUI'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : System.getenv('bintray_user')
	key = project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : System.getenv('bintray_key')
	publish = true
	override = true
	publications = ['mavenJava']
	pkg {
		repo = project.bintrayRepo
		name = project.name
		user = bintray.user
		userOrg = project.repoOwner
		licenses = [project.codeLicense]
		desc = project.description
		websiteUrl = "https://github.com/${project.repoOwner}/${project.repoName}"
		issueTrackerUrl = "https://github.com/${project.repoOwner}/${project.repoName}/issues"
		vcsUrl = "https://github.com/${project.repoOwner}/${project.repoName}.git"
		publicDownloadNumbers = true

		githubRepo = "${project.repoOwner}/${project.repoName}"
		version {
			name = project.version
			released = new Date()
		}
	}
}
publishing {
	publications {
		mavenJava(MavenPublication) {
			pom {
				name = project.name
				description = project.description
				url = "https://github.com/${project.repoOwner}/${project.repoName}"
				licenses {
					license {
						name = project.codeLicense
					}
				}

				scm {
					url = "https://github.com/${project.repoOwner}/${project.repoName}"
				}
			}

			// add all the jars that should be included when publishing to maven
			afterEvaluate {
				artifact jar
				artifact sourcesJar
			}
		}
	}
}